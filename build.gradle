buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://plugins.gradle.org/m2/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: szewek.mcgen.MCGenPlugin

group = 'szewek.flux'
archivesBaseName = 'flux'

project.version = project.version + ((project.versionSub as int) > 0 ? "-${project.versionType}${project.versionSub}" : "")

String core_version = "3.0.5"
String metals_version = "1.0.0"

String mc_version = "1.15.2"
String jei_version = "6.0.0.2"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

idea {
    module {
        inheritOutputDirs = true
        for (String excludeDirName in ["run", "out", "logs", "gradle"]) {
            File excludeDir = new File(projectDir, excludeDirName)
            excludeDirs.add(excludeDir)
        }
    }
}
sourceSets {
    core { java { srcDir 'src/core/java' } }
    metals {
        java { srcDir 'src/metals/java' }
        compileClasspath += sourceSets.core.output
    }
    main { compileClasspath += sourceSets.core.output }
}
configurations {
    def extendCompile = [coreCompile, metalsCompile]
    def extendCompileOnly = [coreCompileOnly, metalsCompileOnly]
    def extendRuntimeOnly = [coreRuntimeOnly, metalsRuntimeOnly]
    for (def c : extendCompile) c.extendsFrom(compile)
    for (def co : extendCompileOnly) co.extendsFrom(compileOnly)
    for (def ro : extendRuntimeOnly) ro.extendsFrom(runtimeOnly)
}

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'snapshot', version: '20200129-1.15.1'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                flux { source sourceSets.main }
                fl { source sourceSets.core }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                flux { source sourceSets.main }
                fl { source sourceSets.core }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'flux', '--all', '--output', file('src/generated/resources/')

            mods {
                flux { source sourceSets.main }
                fl { source sourceSets.core }
            }
        }
    }
}

processResources {
    inputs.property('modVersion', project.version)
    from (sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand 'modVersion': project.version
    }
    from (sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}
processCoreResources {
    inputs.property('coreVersion', core_version)
    from (sourceSets.core.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand 'coreVersion': core_version
    }
    from (sourceSets.core.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}

repositories {
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:$mc_version-$forge_version"

    compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}")

    // You may put jars on which you depend on in ./libs or you may define them like so..
    // compile "some.group:artifact:version:classifier"
    // compile "some.group:artifact:version"

    // The 'provided' configuration is for optional dependencies that exist at compile-time but might not at runtime.
    // provided 'com.mod-buildcraft:buildcraft:6.0.8:dev'

    // These dependencies get remapped to your current MCP mappings
    // deobf 'com.mod-buildcraft:buildcraft:6.0.8:dev'
}

jar {
    manifest {
        attributes([
            "Specification-Title": "flux",
            "Specification-Vendor": "Szewek",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": "Flux",
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"Szewek",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task coreJar(type: Jar) {
    archiveName = "fl-${core_version}.jar"
    from sourceSets.core.output
}
task metalsJar(type: Jar) {
    archiveName = "fluxmetals-${metals_version}.jar"
    from sourceSets.metals.output
}

// Example configuration to allow publishing using the maven-publish task
// we define a custom artifact that is sourced from the reobfJar output task
// and then declare that to be published
// Note you'll need to add a repository here
def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}

artifacts {
    archives coreJar
    archives metalsJar
}

reobf {
    coreJar { classpath = sourceSets.core.compileClasspath }
    metalsJar { classpath = sourceSets.metals.compileClasspath }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

curseforge {
    apiKey = project.hasProperty("cfKey") ? project.cfKey : ""
    project { // Flux
        id = "248942"
        releaseType = project.versionType
        changelogType = "markdown"
        changelog = file("latest.md")
        relations {
            requiredDependency "fl"
            optionalDependency "jei"
        }
    }
    project { // Flux Library
        id = "262385"
        releaseType = "release"
        changelogType = "markdown"
        changelog = file("latest-fl.md")
        mainArtifact(coreJar) {

        }
    }
}
repositories {
    mavenCentral()
}